#version 460 core

/** 
 * This is a compute shader responsible for the Ray Tracing
 * - in the first part there are defined struct used throughout the shader
 * - most of these struct MUST be exactly the same as in the c++ code when being sent by the CPU
 */

// CONSTANTS
#define AABB_primitives_limit 2
#define MAX_STACK_SIZE 28 // (BVH tree depth)

#define heatmap_cold vec3(0.0, 0.0, 0.0)
#define heatmap_warm vec3(0.9, 1.0, 0.9)

// work group sizes
#define LOCAL_GROUP_X 8
#define LOCAL_GROUP_Y 4
#define LOCAL_GROUP_Z 1

#define NUM_SPHERES 1

#define PI 3.1415926
#define EPSILON 1.0e-10 // Small value to avoid division by zero
#define INF 1.0 / 0.0;

/** The BVHNode struct represents a node in the Bounding Volume Hierarchy (BVH) tree.
 * The BVH tree is used to organize the triangles in the scene into a hierarchy of axis-aligned bounding boxes (AABBs).
 * Each BVH node contains information about the bounding box of the node, as well as references to the child nodes or the triangles contained in the node.
 */
struct BVHNode
{
    // Each integer is treated as 16 bytes.
    int leaf_primitive_indices[AABB_primitives_limit];     // offset 0 // alignment 64 // size 64 // total 64 bytes

    vec3 minVec;        // offset 64  // alignment 64  // size 60  // total 76 bytes
    int child1_idx;     // offset 80  // alignment 52  // size 52  // total 80 bytes
    vec3 maxVec;        // offset 80  // alignment 64  // size 60  // total 92 bytes
    int child2_idx;     // offset 92  // alignment 52  // size 52  // total 96 bytes
};

/** The RaytracingMaterial struct represents the material properties of an object in the scene.
 * The material properties include the color of the object, the strength and color of the emission, and padding for alignment.
 */
struct RaytracingMaterial
{
    vec3 color;                 // offset 0   // alignment 16 // size 12 // total 12 bytes
    float emissionStrength;     // offset 12  // alignment 4  // size 4  // total 16 bytes
    vec3 emissionColor;         // offset 16  // alignment 16 // size 12 // total 28 bytes
    float std140padding;        // offset 28  // alignment 4  // size 4  // total 32 bytes
};

struct Sphere {
	RaytracingMaterial material;    // offset 0   // alignment 16 // size 32 // total 32 bytes
	vec3 position;                  // offset 32  // alignment 16 // size 12 // total 44 bytes
	float radius;                   // offset 44  // alignment 4  // size 4  // total 48 bytes
};

struct Triangle
{
    // vertices
    vec3 v1;                    // offset 0   // alignment 16 // size 12 // total 16 bytes 
    float std140padding1;       // offset 12  // alignment 4  // size 4  // total 16 bytes
    vec3 v2;                    // offset 16  // alignment 16 // size 12 // total 32 bytes 
    float std140padding2;       // offset 28  // alignment 4  // size 4  // total 32 bytes
    vec3 v3;                    // offset 32  // alignment 16 // size 12 // total 48 bytes 
    float std140padding3;       // offset 44  // alignment 4  // size 4  // total 48 bytes
    
    //normals
    vec3 NA;                    // offset 48  // alignment 16 // size 12 // total 64 bytes 
    float std140padding4;       // offset 60  // alignment 4  // size 4  // total 64 bytes
    vec3 NB;                    // offset 64  // alignment 16 // size 12 // total 80 bytes 
    float std140padding5;       // offset 76  // alignment 4  // size 4  // total 80 bytes
    vec3 NC;                    // offset 80  // alignment 16 // size 12 // total 96 bytes 
    float std140padding6;       // offset 92  // alignment 4  // size 4  // total 96 bytes
    
    vec3 centroid_vec;          // offset 96  // alignment 16 // size 12 // total 112 bytes
    float std140padding7;       // offset 108 // alignment 4  // size 4  // total 112 bytes

    RaytracingMaterial material;    // offset 112 // alignment 16 // size 32 bytes // total 148 bytes
};


/** The Ray struct represents a ray in the scene, defined by an origin point and a direction vector.
 * The ray is used to trace the path of light through the scene and calculate intersections with objects.
 * Each pixel in the image corresponds to a ray that is traced through the scene to determine the color of the pixel.
 */
struct Ray
{
    vec3 origin;
    vec3 dir;
};

/** The HitInfo struct contains information about a ray-object intersection, including whether the ray collided with an object,
 * the distance to the collision point, the position of the collision point, the surface normal at the collision point, and the material properties of the object.
 */
struct HitInfo
{
    bool didCollide;
    float dst;
    vec3 hitPoint;
    vec3 normal;
    RaytracingMaterial material;
};

// work group sizes
layout (local_size_x = LOCAL_GROUP_X, 
        local_size_y = LOCAL_GROUP_Y, 
        local_size_z = LOCAL_GROUP_Z) in;

layout (rgba32f, binding = 0) uniform image2D rayTracingTexture;

//UBOs
layout (std140, binding = 0) uniform uniformParameters {
    uint u_numAccumulatedFrames;    // offset 0  // alignment 4 // total 4 bytes
    uint RAYS_PER_PIXEL_COUNT;      // offset 4  // alignment 4 // total 8 bytes
    uint RAY_BOUNCE_COUNT;          // offset 8  // alignment 4 // total 12 bytes
    float u_FocalLength;            // offset 12 // alignment 4 // total 16 bytes

    vec3 u_skyboxGroundColor;       // offset 16 // alignment 16 // total 32 bytes
    vec3 u_skyboxHorizonColor;      // offset 32 // alignment 16 // total 48 bytes
    vec3 u_skyboxZenithColor;       // offset 48 // alignment 16 // total 64 bytes                            
    vec3 u_CameraPos;               // offset 64 // alignment 16 // total 80 bytes

    mat4 u_ModelMatrix;             // offset 80 // alignment 16 // total 144 bytes

    bool u_WasInput;                // offset 144 // alignment 4 // total 148 bytes

    bool u_displayBVH;               // offset 148 // alignment 4 // total 152 bytes
    bool u_displayMultipleBVHlayers; // offset 152 // alignment 4 // total 156 bytes
    uint u_BVHlayerToDisplay;        // offset 156 // alignment 4 // total 160 bytes
    uint u_BVHTreeDepth;             // offset 160 // alignment 4 // total 164 bytes
    bool u_show_skybox;              // offset 164 // alignment 4 // total 168 bytes
};

layout (std140, binding = 1) uniform sceneBuffer
{
    Sphere u_Spheres[NUM_SPHERES];
};


/** The MESH_buffer SSBO stores the triangles that make up the mesh in the scene.
 * The triangles are stored in an array of Triangle structs, where each Triangle struct contains the vertices of the triangle, the normals at each vertex, and the material properties of the triangle.
 */
layout (std140, binding = 3) buffer MESH_buffer
{
    Triangle MESH[];
};

/** The BVH_buffer SSBO stores the nodes of the Bounding Volume Hierarchy (BVH) tree that organizes the triangles in the scene.
 * The BVH tree is used to optimize ray-triangle intersection tests by reducing the number of triangles that need to be checked for intersection with a given ray.
 * The BVH tree is stored in an array of BVHNode structs, where each BVHNode struct contains information about the bounding box of the node, as well as references to the child nodes or the triangles contained in the node.
 */
layout (std140, binding = 4) buffer BVH_buffer
{
    BVHNode BVH[];
};


/** The function getCurrentState calculates a unique state value based on the texel coordinates and the number of accumulated frames.
 * The state value is used to generate random numbers for sampling in the shader.
 */
uint getCurrentState(ivec2 texelCoords, int screenWidth)
{
    uint pixelIndex = (uint(texelCoords.y) * uint(screenWidth)) + uint(texelCoords.x);
    return pixelIndex + u_numAccumulatedFrames * 745621; // new state every frame
}

float RandomValue(inout uint state)
{
    state = state * 747796405u + 2891336453u;
    uint result = ((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
    result = (result >> 22u) ^ result;
    return float(result) / 4294967295.0;
}

/** The RandomValueNormalDistribution function generates a random value from a normal distribution using the Box-Muller transform.
 * The function generates two random values from a uniform distribution and transforms them into a random value from a normal distribution.
 */
float RandomValueNormalDistribution(inout uint state)
{
    // Thanks to https://stackoverflow.com/a/6178290
    // And Sebastian Lague
    float theta = 2 * PI * RandomValue(state);
    float rho = sqrt(-2 * log(RandomValue(state)));
    return rho * cos(theta);
}

/** The RandomDirection function generates a random direction vector by sampling from a normal distribution in three dimensions.
 * The function generates three random values from a normal distribution and normalizes them to create a random direction vector.
 */
vec3 RandomDirection(inout uint state)
{
    // thanks to https://math.stackexchange.com/questions/1585975 and Sebastian Lague
    float x = RandomValueNormalDistribution(state);
    float y = RandomValueNormalDistribution(state);
    float z = RandomValueNormalDistribution(state);
    return normalize(vec3(x, y, z));
}


/** The RandomDirectionInHemisphere function generates a random direction vector in the hemisphere defined by the normal vector.
 * The distribution is cosine-weighted. (meaning more rays are sent in the direction of the normal)
 */
vec3 RandomDirectionInHemisphere(vec3 normalVector, inout uint state)
{
    vec3 randomDirectionVector = RandomDirection(state);
    if (dot(normalVector, randomDirectionVector) < 0)
    {
        randomDirectionVector = -randomDirectionVector;
    }
    return randomDirectionVector;
}

/** The GainSkyboxLight function calculates the color of the skybox based on the direction of the ray.
 * The function uses a gradient from the horizon color to the zenith color to simulate the sky.
 */
vec3 GainSkyboxLight(const Ray ray)
{
    // Environment Settings
    if (!u_show_skybox)
    {
        return vec3(0.0);
    }
    float skyGradientT = pow(smoothstep(0.0, 0.4, ray.dir.y), 0.35);
    float groundToSkyT = smoothstep(-0.01, 0.0, ray.dir.y);
    vec3 skyGradient = mix(u_skyboxHorizonColor.rgb, u_skyboxZenithColor.rgb, skyGradientT);
    skyGradient = mix(u_skyboxGroundColor.rgb, skyGradient, groundToSkyT);
    return skyGradient;
}

/** The RayAABBIntersection function checks if a ray intersects an axis-aligned bounding box (AABB).
 * The function uses the slab method to determine if the ray intersects the AABB.
 * Fast, Branchless Ray/Bounding Box Intersections
 * Source: https://tavianator.com/fast-branchless-raybounding-box-intersections/
 */
bool RayAABBIntersection(Ray ray, const vec3 minVec, const vec3 maxVec, inout float largest_tMin)
{
    const vec3 t1 = (minVec - ray.origin) / ray.dir;
    const vec3 t2 = (maxVec - ray.origin) / ray.dir;

    const vec3 tMin = min(t1, t2);
    const vec3 tMax = max(t1, t2);

    largest_tMin = max(max(tMin.x, tMin.y), tMin.z);
    const float smallest_tMax = min(min(tMax.x, tMax.y), tMax.z);

    return smallest_tMax >= largest_tMin && smallest_tMax >= 0.0;
}

/** The RaySphereIntersection function checks if a ray intersects a sphere.
 * The function uses the quadratic formula to determine if the ray intersects the sphere.
 * If the ray intersects the sphere, the HitInfo struct.
 * Source: Sebastian Lague - https://youtu.be/Qz0KTGYJtUk?t=321
 */
HitInfo RaySphereIntersection(Ray ray, const vec3 spherePosition, const float sphereRadius)
{
    HitInfo hitInfo;
    hitInfo.didCollide = false;
    
    vec3 offsetRayOrigin = ray.origin - spherePosition;
    // from the equation: sqrt(length(rayOrigin + rayDirection * dst)) = radius^2
    float a = dot(ray.dir, ray.dir); //(a = 1)
    float b = 2 * dot(offsetRayOrigin, ray.dir);
    float c = dot(offsetRayOrigin, offsetRayOrigin) - sphereRadius * sphereRadius;
    // quadratic discriminant
    float discriminant = b * b - 4 * a * c; // b^2-4ac
    if (discriminant >= 0)
    {
        // nearest sphere intersect
        float dst = (-b - sqrt(discriminant)) / 2;
        
        // if the intersection did not happen behind the camera
        if (dst >= 0)
        {
            hitInfo.didCollide = true;
            hitInfo.dst = dst;
            hitInfo.hitPoint = ray.origin + (ray.dir * dst);
            hitInfo.normal = normalize(hitInfo.hitPoint - spherePosition);
        }
    }
    return hitInfo;
}

/** The RayTriangleIntersection function checks if a ray intersects a triangle.
 * The function uses the Möller–Trumbore intersection algorithm to determine if the ray intersects the triangle.
 * If the ray intersects the triangle, the HitInfo struct is returned with information about the intersection.
 * Source: https://stackoverflow.com/questions/42740765/intersection-between-line-and-triangle-in-3d/42752998#42752998
 */
HitInfo RayTriangleIntersection(const Ray ray, const Triangle tri)
{
    const vec3 E1 = tri.v2 - tri.v1;
    const vec3 E2 = tri.v3 - tri.v1;
    vec3 triNormal = cross(E1, E2);

    const float determinant = -dot(ray.dir, triNormal);

    // Early exit if the ray and triangle are nearly parallel
    if (determinant < 1E-6)
    {
        HitInfo hitInfo;
        hitInfo.didCollide = false;
        return hitInfo;
    }

    const float invdet = 1.0 / determinant;
    const vec3 AO = ray.origin - tri.v1;
    const vec3 DAO = cross(AO, ray.dir);

    const float t = dot(AO, triNormal) * invdet;
    const float u = dot(E2, DAO) * invdet;
    const float v = -dot(E1, DAO) * invdet;
    const float w = 1 - u - v;

    // Back-face culling (assuming triangles are consistently oriented)
    if (t < 0 || u < 0 || v < 0 || w < 0)
    {
        HitInfo hitInfo;
        hitInfo.didCollide = false;
        return hitInfo;
    }

    HitInfo hitInfo;
    hitInfo.didCollide = true;
    hitInfo.hitPoint = ray.origin + ray.dir * t;
    hitInfo.normal = normalize(tri.NA * w + tri.NB * u + tri.NC * v);
    hitInfo.dst = t;
    return hitInfo;
}




/** The BVH_traverse function traverses the Bounding Volume Hierarchy (BVH) tree to find the closest intersection of a ray with the objects in the scene.
 * The function uses a stack to keep track of the nodes that need to be checked for intersection.
 * The function iterates over the nodes in the BVH tree and checks for intersections with the bounding boxes of the nodes.
 * If the ray intersects a leaf node, the function checks for intersections with the primitives contained in the node.
 * If the ray intersects a primitive, the function updates the closest intersection found so far.
 *
 *   Optimizations applied:
 * - inlined stack functions & not using the stack struct but variables directly
 * - declaring the node and triangle as const
 * - (not implemented yet) depth testing of AABBs to avoid unnecessary intersection tests
 */
void BVH_traverse(Ray ray, inout HitInfo closestHit, inout vec3 accum_AABB_clr)
{
    // A stack is initialized to keep track of the BVH nodes that need to be checked.
    int stack_elements[MAX_STACK_SIZE];
    int stack_top = -1;

    if (stack_top < MAX_STACK_SIZE - 1) { // Inlined stack_push
        stack_top++;
        stack_elements[stack_top] = 0; // The root node of the BVH is pushed onto the stack.
    }

    uint intersections_count = 0;
    float AABB_tMin;
    
    // The function continues to check nodes as long as there are nodes left on the stack.
    while (stack_top >= 0) // Inlined !stack_empty(stack)
    {
        // The next node to check is popped from the stack.
        int current_node_idx;
        if (stack_top >= 0) { // Inlined stack_pop
            current_node_idx = stack_elements[stack_top];
            stack_top--;
        }

        const BVHNode current_node = BVH[current_node_idx];

        // The function checks if the ray intersects the bounding box of the current node.
        if (RayAABBIntersection(ray, current_node.minVec, current_node.maxVec, AABB_tMin))
        {
            if (AABB_tMin > closestHit.dst)
			{
				continue;
			}
            
            // If the current node is a leaf node (it has no children), the function 
            // checks for intersections with the primitives contained in the node.
            if (current_node.child1_idx == -1 && current_node.child2_idx == -1)
            {
                // The function iterates over each primitive in the leaf node.
                for (int i = 0; i < AABB_primitives_limit; i++)
                {
                    const int triangle_idx = current_node.leaf_primitive_indices[i];
                    
                    // If there are no more primitives in the node, the 
                    // function breaks out of the loop.
                    if (triangle_idx == -1)
                    {
                        break;
                    }

                    // The function retrieves the triangle associated with the current primitive.
                    const Triangle tri = MESH[triangle_idx];
                                  
                    const HitInfo triHitInfo = RayTriangleIntersection(ray, tri);
                    
                    // If the ray intersects the triangle and the intersection is closer than the
                    // closest intersection found so far, the closest intersection is updated.
                    if (triHitInfo.didCollide && triHitInfo.dst < closestHit.dst)
                    {
                        closestHit = triHitInfo;
                        closestHit.material = tri.material;
                        break;
                    }
                }
            }
            else
            {
                intersections_count += 1;

                if (current_node.child1_idx != -1) { // Inlined stack_push
                    if (stack_top < MAX_STACK_SIZE - 1) {
                        stack_top++;
                        stack_elements[stack_top] = current_node.child1_idx;
                    }
                }

                if (current_node.child2_idx != -1) { // Inlined stack_push
                    if (stack_top < MAX_STACK_SIZE - 1) {
                        stack_top++;
                        stack_elements[stack_top] = current_node.child2_idx;
                    }
                }
            }
        }
    }
    if (u_displayBVH)
	{
        if (u_displayMultipleBVHlayers && intersections_count <= u_BVHlayerToDisplay && intersections_count > 0){
            float t1 = pow(float(intersections_count) / float(u_BVHlayerToDisplay), 1);
            accum_AABB_clr = mix(heatmap_cold, heatmap_warm, t1);
        }
        else if (u_BVHlayerToDisplay <= intersections_count){
            accum_AABB_clr = heatmap_warm;
        }
	}
}


/** The CheckRayCollision function traces a ray through the scene and checks for intersections with the objects in the scene.
 * The function iterates over each sphere in the scene and checks for intersections.
 * The function traverses the BVH to find the closest intersection of the ray with the objects in the scene.
 * If an intersection is found, the function returns information about the intersection, including the material properties of the object.
 */
HitInfo CheckRayCollision(Ray ray, inout HitInfo closestHit, inout vec3 accum_AABB_clr)
{
    closestHit.didCollide = false;
    closestHit.dst =  INF; // infinity

    // The function iterates over each sphere in the scene and checks for intersections.
    for (int i = 0; i < NUM_SPHERES; i++)
    {
        Sphere sphere = u_Spheres[i];
    
        HitInfo hitInfo = RaySphereIntersection(ray, sphere.position, sphere.radius);
        if (hitInfo.didCollide && hitInfo.dst < closestHit.dst)
        {
            closestHit = hitInfo;
            closestHit.material = sphere.material;
        }
    }   
    
    // The function traverses the BVH to find the closest intersection of the ray with the objects in the scene.
    BVH_traverse(ray, closestHit, accum_AABB_clr); // traversing all the triangles
    
    return closestHit;
}

/** The TraceRay function traces a ray through the scene and calculates the color of the ray based on the objects it intersects.
 * The function iterates over each bounce of the ray and calculates the color of the ray based on the material properties of the objects it intersects.
 * The function uses the CheckRayCollision function to find the closest intersection of the ray with the objects in the scene.
 * The function calculates the color of the ray based on the material properties of the objects it intersects.
 */
vec3 TraceRay(Ray ray, inout uint state, inout vec3 accum_AABB_clr)
{
    vec3 rayColor = vec3(1.0);
    vec3 incomingLight = vec3(0.0);
    HitInfo hitInfo;
    

    for (int i = 0; i <= RAY_BOUNCE_COUNT; i++)
    {
        CheckRayCollision(ray, hitInfo, accum_AABB_clr);
        if (hitInfo.didCollide)
        {
            ray.origin = hitInfo.hitPoint;
            ray.dir = normalize(hitInfo.normal + RandomDirection(state));
            
            RaytracingMaterial material = hitInfo.material;
            vec3 emittedLight = material.emissionColor * material.emissionStrength;
            
            // this is when the ray gets color
            incomingLight += emittedLight * rayColor;
            rayColor *= material.color;
        }
        else
        {
            incomingLight += GainSkyboxLight(ray) * rayColor;
            break;
        }
    }
    return incomingLight;
}

/** The main function of the compute shader is responsible for tracing rays through the scene and calculating the color of the pixels in the image.
 * The function is called for each pixel in the image and traces a ray through the scene to calculate the color of the pixel.
 * The function accumulates the color of the pixel over multiple frames to reduce noise in the image overtime.
 */
void main()
{
    // getting the coordinates of the current texel (pixel color texture coordinates)
    ivec2 texelCoords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 dims = imageSize(rayTracingTexture);
    float x = (float(texelCoords.x * 2 - dims.x) / dims.x); // transforms to [-1.0, 1.0]
    float y = (float(texelCoords.y * 2 - dims.y) / dims.x); // deviding by x to keep the ratio
    
    uint state = getCurrentState(texelCoords, dims.x);
    
    // applying transformation (camera rotation)
    Ray ray;
    ray.dir = normalize(vec3(x, y, u_FocalLength));
    ray.dir = (u_ModelMatrix * vec4(ray.dir, 1.0f)).rgb; // apply the rotation transformation of the camera
    ray.origin = u_CameraPos.xyz;
    vec3 accum_AABB_clr = vec3(0.0); 

    // The actual tracing of the ray
    vec3 tracingResult = vec3(0.0);
    for (int i = 0; i < RAYS_PER_PIXEL_COUNT; i++)
    {
        tracingResult += TraceRay(ray, state, accum_AABB_clr);
    }

    // averaging the current frame accumulated pixel color
    if (u_displayBVH)
	{
        tracingResult = accum_AABB_clr / RAYS_PER_PIXEL_COUNT;
    }
    else {
		tracingResult = tracingResult / RAYS_PER_PIXEL_COUNT;
	}

    vec4 accumulatedColor = imageLoad(rayTracingTexture, texelCoords);
    
    // averaging the previous & current frame pixel color
    float weight = 1.0f / (u_numAccumulatedFrames + 1);
    vec3 outputColor = accumulatedColor.rgb * (1 - weight) + tracingResult * weight;
    
	imageStore(rayTracingTexture, texelCoords, vec4(outputColor, 1.0f));

};